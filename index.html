<!DOCTYPE html>
<html >
  <head>
    <meta charset="UTF-8">
    <title>Cardboard Clouds</title>
  </head>
  <body>
    <div id="container"></div>
    <div id="stats">
      <div id="position"></div>
      <div id="rotation"></div>
    </div>
    <div id="credits">
      adapted from: <a href="https://www.shadertoy.com/view/XslGRr">iq@shadertoy</a>
    </div>

<style>
body, html, #container {
  padding: 0;
  margin: 0;
  overflow: hidden;
}
#stats {
  position: absolute;
  top: 0;
  left: 0;
  font-size: 18px;
  font-family: monospace;
}
#credits {
  position: absolute;
  bottom: 0;
  right: 0;
  font-size: 10px;
}
</style>

<script id="vertexShader" type="x-shader/x-vertex">
  void main() {
    gl_Position = vec4( position, 1.0 );
  }
</script>

<script id="fragmentShader" type="x-shader/x-fragment">
  uniform vec2 iResolution;
  uniform float iGlobalTime;
  uniform vec3 iDirection;
  uniform vec3 iPosition;
  uniform sampler2D iChannel0;

  const float fieldOfView = 0.6;

  // Created by inigo quilez - iq/2013
  // License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

  // Volumetric clouds. It performs level of detail (LOD) for faster rendering

  float noise(in vec3 x) {
    vec3 p = floor(x);
    vec3 f = fract(x);
    f = f*f*(3.0-2.0*f);
    vec2 uv = (p.xy+vec2(37.0,17.0)*p.z) + f.xy;
    vec2 rg = texture2D(iChannel0, (uv+ 0.5)/256.0, -100.0).yx;
    return -1.0+2.0*mix(rg.x, rg.y, f.z);
  }

  float map5(in vec3 p) {
    vec3 q = p;
    float f;
    f  = 0.50000*noise(q); q = q*2.02;
    f += 0.25000*noise(q); q = q*2.03;
    f += 0.12500*noise(q); q = q*2.01;
    f += 0.06250*noise(q); q = q*2.02;
    f += 0.03125*noise(q);
    return clamp(1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0);
  }

  float map4(in vec3 p) {
    vec3 q = p;
    float f;
    f  = 0.50000*noise(q); q = q*2.02;
    f += 0.25000*noise(q); q = q*2.03;
    f += 0.12500*noise(q); q = q*2.01;
    f += 0.06250*noise(q);
    return clamp(1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0);
  }

  float map3(in vec3 p) {
    vec3 q = p;
    float f;
    f  = 0.50000*noise(q); q = q*2.02;
    f += 0.25000*noise(q); q = q*2.03;
    f += 0.12500*noise(q);
    return clamp(1.5 - p.y - 2.0 + 1.75*f, 0.0, 1.0);
  }

  vec3 sundir = normalize(vec3(-1.0,0.0,-1.0));

  vec4 integrate(in vec4 sum, in float dif, in float den, in vec3 bgcol, in float t) {
    // lighting
    vec3 lin = vec3(0.65,0.7,0.75)*1.4 + vec3(1.0, 0.6, 0.3)*dif;
    vec4 col = vec4(mix(vec3(1.0,0.95,0.8), vec3(0.25,0.3,0.35), den), den);
    col.xyz *= lin;
    col.xyz = mix(col.xyz, bgcol, 1.0-exp(-0.003*t*t));
    // front to back blending
    col.a *= 0.4;
    col.rgb *= col.a;
    return sum + col*(1.0-sum.a);
  }

  #define MARCH(STEPS,MAPLOD) for(int i=0; i<STEPS; i++) { vec3  pos = ro + t*rd; if(sum.a > 0.99) break; float den = MAPLOD(pos); if(den>0.01) { float dif =  clamp((den - MAPLOD(pos+0.3*sundir))/0.6, 0.0, 1.0); sum = integrate(sum, dif, den, bgcol, t); } t += max(0.05,0.02*t); }

  vec4 raymarch(in vec3 ro, in vec3 rd, in vec3 bgcol) {
    vec4 sum = vec4(0.0);
    float t = 0.0;
    MARCH(20,map5);
    MARCH(30,map4);
    MARCH(40,map3);
    return clamp(sum, 0.0, 1.0);
  }

  vec4 render(in vec3 ro, in vec3 rd) {
    // background sky
    float sun = clamp(dot(sundir,rd), 0.0, 1.0);
    vec3 col = vec3(0.6,0.71,0.75) - rd.y*0.2*vec3(1.0,0.5,1.0) + 0.15*0.5;
    col += 0.2*vec3(1.0,.6,0.1)*pow(sun, 8.0);

    // clouds
    vec4 res = raymarch(ro, rd, col);
    col = col*(1.0-res.w) + res.xyz;

    // sun glare
    col += 0.2*vec3(1.0,0.4,0.2)*pow(sun, 3.0);

    return vec4(col, 1.0);
  }

  void mainImage(out vec4 fragColor, in vec2 fragCoord) {
    vec2 halfRes = iResolution * vec2(0.5, 1.0);
    vec2 halfCoord = mod(fragCoord, halfRes);
    vec2 pixel = (-halfRes + 2.0 * halfCoord) / halfRes.y;

    // inefficient way to get xyz axis
    vec3 rz = vec3(cos(iDirection.z)*sin(iDirection.x), sin(iDirection.z), cos(iDirection.z)*cos(iDirection.x)); // into
    vec3 vx = normalize(cross(rz, vec3(0.0, 1.0, 0.0))); // right
    vec3 vy = normalize(cross(vx, rz)); // up
    vec3 rx = cos(-iDirection.y) * vx + sin(-iDirection.y) * vy;
    vec3 ry = -sin(-iDirection.y) * vx + cos(-iDirection.y) * vy;
    vec3 origin = iPosition + sign(fragCoord.x - iResolution.x / 2.0) * 0.1 * rx; // origin is offset by eye

    if(length(pixel) < 1.1) {
      vec3 ray = normalize(rx*pixel.x + ry*pixel.y + rz*fieldOfView);
      fragColor = render(origin, ray);
    } else {
      // save gpu
      fragColor = vec4(0.65, 0.95, 1.0, 1.0);
    }
  }

  void main(void) {
    mainImage(gl_FragColor, gl_FragCoord.xy);
  }
</script>

<script src='three.js'></script>
<script src="index.js"></script>

  </body>
</html>
